plugins {
    id 'java'
    id 'com.adarshr.test-logger' version '3.1.0'
    id 'edu.sc.seis.launch4j' version '2.5.1'
}

group = 'hu.mmagyar'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    javaMainClass = "hu.mmagyar.engine.Main"

    lwjglVersion = "3.3.0"
    jomlVersion = "1.10.3"
    lwjglNatives = "natives-windows"
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-nfd"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nfd::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    //noinspection GroovyAssignabilityCheck
    manifest {
        attributes(
                'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                'Main-Class': javaMainClass
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task runJar(type: Exec) {
    dependsOn jar
    group = "Execution"
    description = "Run the mainClass from the output jar in classpath with ExecTask"
    commandLine "java", "-classpath", jar.archiveFile.get(), javaMainClass
}

apply plugin: 'edu.sc.seis.launch4j'
launch4j {
    mainClassName = javaMainClass
    bundledJrePath = './jre/'
    bundledJre64Bit = true
}

task addJreToDistributable(type: Copy) {
    from zipTree("src/main/resources/desktop-deployment/jdk-17.0.2.zip")
    destinationDir = file("$buildDir/launch4j/jre")
}
addJreToDistributable.dependsOn createExe

test {
    useJUnitPlatform()
    testlogger {
        theme 'plain'
        showExceptions true
        showStackTraces true
        showFullStackTraces false
        showCauses true
        slowThreshold 2000
        showSummary true
        showSimpleNames false
        showPassed true
        showSkipped true
        showFailed true
        showStandardStreams false
        showPassedStandardStreams true
        showSkippedStandardStreams true
        showFailedStandardStreams true
        logLevel 'lifecycle'
    }
}